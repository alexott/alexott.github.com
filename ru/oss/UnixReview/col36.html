<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML>
<HEAD>
 <META NAME="GENERATOR" CONTENT="SGML-Tools 1.0.9">
 <TITLE>Unix Review Column 36</TITLE>


</HEAD>
<BODY>
Next
Previous
Contents
<HR>
<H1>Unix Review Column 36</H1>

<H2>Randal Schwartz</H2>Февраль 2001<P>[предполагаемый заголовок: 'What is that, anyway?']
<P>Итак, вы получили каталог полный самых разных вещей, или может быть
целое дерево каталогов. Что кроется за каждым из имен? Обозначает ли оно
каталог, символьную ссылку или простой файл? А если это файл, то текстовый
он или двоичный? И если это двоичный файл, то чем он является&nbsp;--
изображением, исполняемым файлом или просто мусором?
<P>В Perl имеется много встроенных операторов для легкого получения списков
имен, а также для определения чем является заданное имя.
<P>Hапример, давайте найдем все подкаталоги внутри текущего каталога:
<P>
<P>
<HR>
<PRE>
  for my $name (glob '*') {
    next unless -d $name;
    print &quot;one directory is $name\n&quot;;
  }
</PRE>
<HR>
<P>Оператор <CODE>glob</CODE> расширяется в список имен файлов не имеющих точки
в начале, а оператор  <CODE>-d</CODE> возвращает истинное значение для тех
имен, которые являются каталогами.
<P>А что если мы хотим выполнить эту операцию рекурсивно? нам необходимо
отойти от использования только основ языка, но не слишком
далеко. Включенный в базовую поставку модуль <CODE>File::Find</CODE> берет на
себя все заботы по рекурсивной обработке каталогов. Давайте найдем все
подкаталоги текущего каталога:
<P>
<HR>
<PRE>
  use File::Find;
  find sub {
    return unless -d $_;
    print &quot;one directory is $File::Find::name\n&quot;;
  }, &quot;.&quot;;
</PRE>
<HR>
<P>Подпрограмма <CODE>find</CODE> принимает в качестве параметра ссылку на
подпрограмму (часто называемую <EM>coderef</EM>), в данном случае
представленную анонимной подпрограммой. Каждое из имен найденное ниже
каталога <CODE>.</CODE> (указанного в последней строке данного кода) будет
вызывать запуск указанной подпрограммы с установкой переменной
<CODE>$File::Find::name</CODE> равной полному имени файла, а <CODE>$_</CODE>
присваивается базовое имя (со сменной текущего каталога на каталог в
котором найден данный файл).
<P>Если вы запустите этот код, то вы увидите, что каждый из каталогов
показан два или более раз! Один раз как имя внутри его родительского
каталога, и один раз как имя текущего каталога <CODE>.</CODE>, когда мы переходим в
каталог, а возможно и больше раз, для каждого из подкаталогов содержащихся
внутри текущего каталога. Как мы можем избежать такого поведения? Хорошо,
просто будем отбрасывать в нашем подкаталоге вхождения для каталогов
<CODE>.</CODE> и <CODE>..</CODE>:
<P>
<HR>
<PRE>
  use File::Find;
  find sub {
    return if $_ eq &quot;.&quot; or $_ eq &quot;..&quot;;
    return unless -d $_;
    print &quot;one directory is $&lt;A HREF="File::Find::name">File::Find::name\n&quot;;
  }, &quot;.&quot;;
</PRE>
<HR>
<P>Вот. Мы будем перемещаться вперед от базового каталога, поскольку
отбрасывание каталогов <CODE>.</CODE> и <CODE>..</CODE> является полезной
вещью.
<P>А что делать с символьными ссылками? Можем ли мы найти их? Конечно! Это
делается с помощью оператора <CODE>-l</CODE>:
<P>
<HR>
<PRE>
  use File::Find;
  find sub {
    return if $_ eq &quot;.&quot; or $_ eq &quot;..&quot;;
    return unless -l $_;
    print &quot;one symlink is $File::Find::name\n&quot;;
  }, &quot;.&quot;;
</PRE>
<HR>
<P>Великолепно! Hо на что они указывают? Существует оператор
<CODE>readlink</CODE>, как в следующем примере:
<P>
<HR>
<PRE>
  use File::Find;
  find sub {
    return if $_ eq &quot;.&quot; or $_ eq &quot;..&quot;;
    return unless -l $_;
    my $dest = readlink($_);
    print &quot;one symlink is $File::Find::name, pointing to $dest\n&quot;;
  }, &quot;.&quot;;
</PRE>
<HR>
<P>Мы можем пропустить тест <CODE>-l</CODE> зная, что любой файл не являющийся
символьной ссылкой автоматически возвращает неопределенное значение при
вызове <CODE>readlink</CODE>, вот так:
<P>
<HR>
<PRE>
  use File::Find;
  my @search = @ARGV;
  @search = qw(.) unless @search;
  find sub {
    return if $_ eq &quot;.&quot; or $_ eq &quot;..&quot;;
    return unless defined (my $dest = readlink($_));
    print &quot;one symlink is $File::Find::name, pointing to $dest\n&quot;;
  }, @search;
</PRE>
<HR>
<P>Я также сделал упрощение для запуска данного скрипта в разных каталогах,
передавая имя базового каталога в командной строке.
<P>Так, что нам осталось сделать? Мы можем обращать внимание и пропускать
каталоги и символические ссылки. А что делать с файлами? Файлы содержат
сами действия. И некоторые из них выглядят как текстовые, а некоторые как
двоичные. Хотя эти строки являются расплывчатыми: вы можете согласиться,
что XML в действительности являются просто текстовыми, а документы
Microsoft Word являются текстом внутри двоичного файла.
<P>Hо сначала вернемся к тому, в чем может помочь Perl. Давайте добавим
оператор  <CODE>-T</CODE> для нахождения этих текстовых файлов:
<P>
<HR>
<PRE>
  use File::Find;
  my @search = @ARGV;
  @search = qw(.) unless @search;
  find sub {
    return if -d $_ or -l $_;
    return unless -T $_;
    print &quot;One text file is $File::Find::name\n&quot;;
  }, @search;
</PRE>
<HR>
<P>И это просто великолепно. Отображает список только текстовых файлов. Hо
это не дает нам подробную информацию. Можем мы хотим отобразить список всех
скриптов на языке Perl. Что нам может дать такую информацию? Хорошо, команда
<EM>file</EM>  из поставки Unix может посмотреть внутрь файла для
определения чем этот файл является. Давайте будем запускать эту команду для
каждого из файлов:
<P>
<HR>
<PRE>
  use File::Find;
  my @search = @ARGV;
  @search = qw(.) unless @search;
  find sub {
    return if -d $_ or -l $_;
    my $file_said = `file $_`;
    if ($file_said =~ /perl/) {
      print &quot;$File::Find::name: $file_said&quot;;
    }
  }, @search;
</PRE>
<HR>
<P>Давайте взглянем на этот код. Теперь мы выбираем имена файлов 
которые команда <EM>file</EM> считает, что они вероятно являются
программами на  Perl. Hо эта программа будет медленно работать на большом
дереве файлов. Поскольку мы запускаем  <EM>file</EM> для каждого из
найденных файлов.
<P>Существует несколько способов ускорения работы. Я мог бы сохранить все
иена файлов и запустить <EM>file</EM> один раз в конце программы:
<P>
<HR>
<PRE>
  use File::Find;
  my @search = @ARGV;
  @search = qw(.) unless @search;
  my @list;
  find sub {
    return if -d $_ or -l $_;
    push @list, $File::Find::name;
  }, @search;
  for (`file @list`) {
    if (/perl/) {
      print;
    }
  }
</PRE>
<HR>
<P>Да, это сильно увеличивает скорость, но мы не получаем результаты до
окончания прохода по дереву файлов, и мы можем столкнуться с проблемой
ограниченности длины командной строки, если мы передаем больше аргументов
чем может обработать <EM>file</EM>.
<P>Hо существует другой способ. Hа CPAN (в таких местах как
<CODE>search.cpan.org</CODE>), мы можем найти модуль <CODE>File::MMagic</CODE>,
который предположительно является модулем на Perl созданным на основе
команды <EM>file</EM>, созданный для проекта PPT и вначале базировавшийся
на коде написанном для Apache для реализации модуля <CODE>mod_mime</CODE>, для
эмуляции стандартной команды <EM>file</EM>. И теперь на основе этого модуля
я могу написать рекурсивную управляемую программу, похожую на
<EM>file</EM>. Будет ли прекращено повторное использование кода? (Я
надеюсь, что нет!)
<P>Все что нам нужно из этого модуля&nbsp;-- это метод с именем
<CODE>checktype_filename</CODE>, который возвращает назад MIME-тип (например
<CODE>text/plain</CODE> или <CODE>image/jpeg</CODE>), и возможно дополнительную
информацию после двоеточия. Так что давайте быстро найдем все скрипты на
Perl. Сначала я смотрю, содержит ли строка слово ``executable'', за которым
следует пробел, затем ищется слово оканчивающееся на ``perl'', за которым
следует пробел, а за ним слово ``script''.  Это простое регулярное
выражение, так что мне просто надо добавить его в нужное место:
<P>
<HR>
<PRE>
  use File::Find;
  use File::MMagic;
  my $mm = File::MMagic-&gt;new;
  my @search = @ARGV;
  @search = qw(.) unless @search;
  my @list;
  find sub {
    return if -d $_ or -l $_;
    my $type = $mm-&gt;checktype_filename($_);
    next unless $type =~ /executable \S+\/perl script/;
    print &quot;$File::Find::name: $type\n&quot;;
  }, @search;
</PRE>
<HR>
<P>Теперь я знаю, какие программы нужно просмотреть когда я буду обновлять
систему, для того чтобы узнать какие модули они используют. (Хмм, звучит
как идея для новой заметки. Я запомню ее).
<P>И последний вариант. Давайте найдем все изображения в дереве файлов и
будем вызывать <CODE>Image::Size</CODE> (который можно найти на CPAN) для
определения их размеров. Совсем немного исправлений:
<P>
<HR>
<PRE>
  use File::Find;
  use File::MMagic;
  use Image::Size;
  my $mm = File::MMagic-&gt;new;
  my @search = @ARGV;
  @search = qw(.) unless @search;
  my @list;
  find sub {
    return if -d $_ or -l $_;
    my $type = $mm-&gt;checktype_filename($_);
    next unless $type =~ /^image\//;
    print &quot;$File::Find::name: $type: &quot;;
    my ($x, $y, $imgtype) = imgsize($_);
    if (defined $x) {
      print &quot;$imgtype: $x x $y\n&quot;;
    } else {
      print &quot;error: $imgtype\n&quot;;
    }
  }, @search;
</PRE>
<HR>
<P>И после того как это заработает, я мог бы убрать использование
<CODE>File::MMagic</CODE> из своей программы, поскольку <CODE>Image::Size</CODE>
может сообщить мне, что он не может быть вызван не для картинок, но вы
знаете старый лозунг Perl: Есть более одного пути сделать это!
<P>Так, что когда в следующий раз вас спросят  ``что у вас есть?'', я
надеюсь, вы сможете ответить на этот вопрос короткой программой на
Perl. Увидимся в следующий раз, наслаждайтесь!
<HR>
Next
Previous
Contents
</BODY>
</HTML>
